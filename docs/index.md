---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "terrahelm Provider"
subcategory: ""
description: |-
  Terrahelm provider allows managing Helm releases using the Helm CLI and
---

# terrahelm Provider

**TerraHelm** is a third-party [Terraform](https://www.terraform.io/) provider that simplifies managing [Helm](https://helm.sh/) releases using [Helm CLI](https://helm.sh/docs/helm/) and providing powerful capabilties, like fetching value files from external sources.

### Features:

- Helm CLI Integration: Seamlessly integrates Helm CLI into Terraform, enabling full access to Helm CLI functionalities for improved debugging and operational flexibility.
- Binary Management: Automatically handles the downloading and installation of the specified Helm binary into a cache_dir directory (`.terraform/terrahelm_cache/` by default). This feature supports effortless version management and switching between different Helm versions.
- Flexible Sources: Supports fetching Helm charts and value files from a variety of sources including Git, Mercurial, HTTP, Amazon S3, and Google Cloud Storage. This flexibility ensures easy integration of custom and third-party Helm charts into your Terraform configuration.
- Enhanced Debugging: Provides the capability to execute Helm CLI commands directly, mirroring the operations performed by the provider. This aids in debugging by allowing users to replicate and diagnose issues within the deployment process.
- Customizable Post-Rendering: Seamlessly integrate post-renderers to inject extra configurations or perform manifest transformations before deployment (like secrets rendering).
- Improved Performance: By directly executing Helm commands, TerraHelm minimizes the overhead imposed by Terraform state management logic, leading to faster deploymentsâ€”especially when managing large-scale or multiple Helm charts concurrently.

## Usage Example

```hcl
# install provider
terraform {
  required_providers {
    terrahelm = {
      source  = "mikhae1/terrahelm"
    }
  }
}

# setup provider
provider "terrahelm" {
  # install given helm cli version locally into `cache_dir`
  helm_version = "v4.1.0"
  kube_context = "kind"
}

# release helm chart
resource "terrahelm_release" "nginx" {
  name       = "nginx"
  namespace  = "nginx"

  # fetch chart from variety of sources: http::, file::, s3::, gcs::, hg::
  chart_url  = "github.com/mikhae1/terraform-provider-terrahelm//tests/charts/?ref=master&depth=1"
  chart_path = "./nginx"

  # fetch value files from various sources
  values_files = [
    "./values/nginx/common.yaml", # relative to chart directory
    "https://raw.githubusercontent.com/mikhae1/terraform-provider-terrahelm/master/tests/charts/values/nginx/dev-values.yaml",
  ]

  # override fetched values from value files
  values = <<EOF
  image:
    tag: "1.25.4
  EOF
}

# retrieve information using data source
data "terrahelm_release" "nginx" {
  name      = "nginx"
  namespace = "nginx"
}
```


<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `cache_dir` (String) Provider cache directory path
- `git_bin_path` (String) Git binary path to use for git clone
- `helm_bin_path` (String) If provided it will be used instead for installing Helm binary
- `helm_version` (String) Helm binary version to install
- `kube_apiserver` (String) Address and the port for the Kubernetes API server
- `kube_as_group` (String) Group to impersonate for the operation, this flag can be repeated to specify multiple groups
- `kube_as_user` (String) Username to impersonate for the operation
- `kube_ca_file` (String) Certificate authority file for the Kubernetes API server connection
- `kube_context` (String) Name of the kubeconfig context to use
- `kube_exec` (Block List, Max: 1) Exec-based authentication configuration for retrieving short-lived Kubernetes tokens without writing kubeconfig credentials. Supports plain token output, ExecCredential JSON (`status.token`), and cloud CLI token JSON (`accessToken`, `access_token`, `token`, `idToken`, `id_token`). Commonly used with managed Kubernetes services like EKS, AKS, and GKE. (see [below for nested schema](#nestedblock--kube_exec))
- `kube_insecure_skip_tls_verify` (Boolean) If true, the Kubernetes API server's certificate will not be checked for validity. This will make your HTTPS connections insecure
- `kube_tls_server_name` (String) Server name to use for Kubernetes API server certificate validation. If it is not provided, the hostname used to contact the server is used
- `kube_token` (String, Sensitive) Bearer token used for authentication
- `kubeconfig` (String) Path to the kubeconfig file

### Nested Schema for `kube_exec`

The `kube_exec` block configures exec-based authentication by executing an external command to retrieve a Kubernetes authentication token. This is useful for managed Kubernetes services that provide CLI tools for token generation.

**Token Output Formats:**

The provider supports multiple token output formats:
- **Plain token**: Raw stdout output containing the token directly
- **ExecCredential JSON**: Kubernetes ExecCredential format with token in `status.token` field
- **Cloud CLI JSON**: Cloud provider CLI output with token in fields like `accessToken`, `access_token`, `token`, `idToken`, or `id_token`

**Required:**

- `command` (String) Command to execute for retrieving the Kubernetes token. Must be the path to an executable binary (e.g., `aws`, `az`, `gcloud`).

**Optional:**

- `api_version` (String) ExecCredential API version passed via `KUBERNETES_EXEC_INFO` environment variable. Used when the command expects Kubernetes ExecCredential format. Common values: `client.authentication.k8s.io/v1beta1`, `client.authentication.k8s.io/v1`.

- `args` (List of String) Arguments to pass to the exec command. Each element is a separate argument.

- `env` (Map of String) Environment variables to pass to the exec command. Merged with existing environment variables.

- `timeout_seconds` (Number) Maximum number of seconds to wait for the exec command before failing. Defaults to `30` seconds.

**Example Usage:**

#### AWS EKS

```hcl
provider "terrahelm" {
  kube_apiserver = "https://your-eks-cluster.us-west-2.eks.amazonaws.com"
  kube_ca_file   = "${path.module}/cluster-ca.crt"

  kube_exec {
    api_version     = "client.authentication.k8s.io/v1beta1"
    command         = "aws"
    args            = ["eks", "get-token", "--cluster-name", "my-cluster"]
    timeout_seconds = 30
  }
}
```

#### Azure AKS

```hcl
provider "terrahelm" {
  kube_exec {
    command = "az"
    args = [
      "account", "get-access-token",
      "--resource", "6dae42f8-4368-4678-94ff-3960e28e3630",
      "--output", "json",
    ]
  }
}
```

#### Google GKE

```hcl
provider "terrahelm" {
  kube_exec {
    command = "gcloud"
    args    = ["auth", "print-access-token"]
  }
}
```

#### Custom Command with Environment Variables

```hcl
provider "terrahelm" {
  kube_exec {
    command = "custom-auth-tool"
    args    = ["--cluster", "my-cluster"]
    env = {
      "AWS_PROFILE" = "production"
      "REGION"      = "us-west-2"
    }
    timeout_seconds = 60
  }
}
```
